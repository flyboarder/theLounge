isAuth() {auth != null}
isCurrent(uid) {isAuth() && auth.uid == uid}
isNull(in) {in == null}
isDefault(in) {isAuth() && in == "default"}

hasUser(tenant, uid) {tenant.users[uid] != null}

roleOwner(tenant, uid)  {isAuth() && tenant.users[uid] == "owner"}
roleGlobal(tenant, uid) {isAuth() && tenant.users[uid] == "global"}
roleAdmin(tenant, uid)  {isAuth() && tenant.users[uid] == "admin"}
roleUser(tenant, uid)   {isAuth() && tenant.users[uid] == "user"}
roleHR(tenant, uid)     {isAuth() && tenant.users[uid] == "hr"}

getUser(uid) {root.users[uid]}
getTenant(id) {root.tenants[id]}
getRole(id) {root.roles[id]}
getSetting(id) {root.settings[id]}

isDegree9() {hasUser(getTenant("degree9"), auth.uid)}

path / is Root;
path /directory is Directory;
path /directory/blocked/{block} is DirectoryBlocked | Null;
path /directory/tenants/{tenant} is DirectoryTenants | Null;
path /directory/pending/{tenant} is DirectoryPending | Null;
path /users/{user} is User;
path /users/{user}/tenants/{tenant} is String | Null;
path /tenants/{tenant} is Tenant;
path /datacenters/{tenant} is DatacenterTenant;
path /datacenters/{tenant}/{datacenter} is Datacenter | Null;
path /datacenters/{tenant}/{datacenter}/actions/{action} is DatacenterAction | Null;
path /datacenters/{tenant}/{datacenter}/triggers/{trigger} is DatacenterTrigger | Null;
path /datacenters/{tenant}/{datacenter}/services/{service} is DatacenterService | Null;
path /datacenters/{tenant}/{datacenter}/services/{service}/actions/{action} is DatacenterAction | Null;
path /services/{tenant} is ServiceTenant;
path /services/{tenant}/{datacenter}/{service} is Service;
path /services/{tenant}/{datacenter}/{service}/instances/{instance} is ServiceInstance;
path /settings/{setting} is Setting;
path /roles/{role} is Role;
path /billing/{tenant} is BillingTenant;
path /notifications/{user} is Notifications;

type SelfSec extends Object {

  read()   {isCurrent(user) || isDefault(user)}
  write()  {isCurrent(user)}
}

type TenantLocalSec extends Object {

  read()   {hasUser(this, auth.uid) || isDefault(tenant)}
  create() {hasUser(this, auth.uid)}
  update() {hasUser(prior(this), auth.uid)}
}

type TenantSec extends Object {

  read()   {hasUser(getTenant(tenant), auth.uid) || isDefault(tenant)}
  create() {hasUser(getTenant(tenant), auth.uid)}
  update() {hasUser(prior(getTenant(tenant)), auth.uid)}
}

type Root extends Object {

  read() {isDegree9()}
}
type Directory extends Object {

  read() {isAuth()}
}

type DirectoryBlocked extends String {
}

type DirectoryTenants extends String {

  create() {isAuth() && isNull(prior(this))}
}

type DirectoryPending extends Object {

  create() {isAuth() && isNull(prior(this))}
}

type User extends SelfSec {
  email: String | Null,
  name: String | Null,
  photo: String | Null,
  phone: String | Null,
  enabled: Boolean | Null,

}

type Tenant extends TenantLocalSec {
  users: Object,
  enabled: Boolean | Null,
  support: String | Null,
  industry: String | Null,
  setup: Boolean | Object | Null

}

type DatacenterTenant extends TenantSec {
}

type Datacenter extends Object {
}

type DatacenterTrigger extends String {
}

type DatacenterService extends Object {
    image: String | Null
}

type DatacenterAction extends Object {
    trigger: String,
    host: String,
    url: String
}

type ServiceTenant extends TenantSec {
}

type Service extends Object {
    settings: Object | Null
}

type ServiceInstance extends Object {
}

type Notifications extends SelfSec {
  info: Notification | Null,
  success: Notification | Null,
  warning: Notification | Null,
  error: Notification | Null

}

type Notification extends Object {
}

type Setting extends Object {
  enabled: Boolean

  read()  {true}
  write() {false}
}

type Role extends Object {

  read()  {true}
  write() {false}
}

type BillingTenant extends TenantSec {
  token: StripeToken,
  customer: StripeCustomer
}

type StripeToken extends Object {
}

type StripeCustomer extends Object {
}
